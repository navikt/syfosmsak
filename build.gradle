buildscript {
    ext.coroutines_version = '0.23.4'
    ext.cxf_version = '3.2.6'
    ext.fellesformat_version = '1.0'
    ext.jaxb_api_version = '2.1'
    ext.jaxb_version = '2.3.0.1'
    ext.jaxws_api_version = '2.2.1'
    ext.kafka_version = '2.0.0'
    ext.kafka_embedded_version = '1.0.0'
    ext.kith_hodemelding_version = '1.1'
    ext.kluent_version = '1.39'
    ext.ktor_version = '0.9.3'
    ext.logstash_logback_encoder = '5.2'
    ext.logback_version = '1.2.3'
    ext.prometheus_version = '0.5.0'
    ext.saaj_version = '1.4.0'
    ext.spek_version = "2.0.0-alpha.1"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id("com.diffplug.gradle.spotless") version "3.14.0"
    id 'application'
}

mainClassName = 'no.nav.syfo.BootstrapKt'

group 'no.nav.syfo'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url = "http://packages.confluent.io/maven/" }
    maven { url 'https://repo.adeo.no/repository/maven-releases/' }
    maven { url "https://dl.bintray.com/spekframework/spek-dev" }
}

applicationDefaultJvmArgs = [
        '-Djavax.net.ssl.trustStore=${NAV_TRUSTSTORE_PATH}',
        '-Djavax.net.ssl.trustStorePassword=${NAV_TRUSTSTORE_PASSWORD}',
        '-Djavax.net.ssl.trustStoreType=JKS'
]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PATH}', '${NAV_TRUSTSTORE_PATH}')
        unixScript.text = unixScript.text.replace('\\${NAV_TRUSTSTORE_PASSWORD}', '${NAV_TRUSTSTORE_PASSWORD}')
    }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        showStandardStreams = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "io.prometheus:simpleclient_common:$prometheus_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_logback_encoder"

    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-features-logging:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"
    implementation "org.apache.kafka:kafka_2.12:$kafka_version"

    implementation "no.nav.syfo.tjenester:fellesformat:$fellesformat_version"
    implementation "no.nav.syfo.tjenester:kith-hodemelding:$kith_hodemelding_version"

    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    implementation "javax.xml.ws:jaxws-api:$jaxws_api_version"
    implementation "com.sun.xml.messaging.saaj:saaj-impl:$saaj_version"

    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "no.nav:kafka-embedded-env:$kafka_embedded_version"

    testImplementation 'org.junit.platform:junit-platform-launcher:1.0.0'
    testRuntimeOnly "org.spekframework.spek2:spek-runtime-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task printVersion() {
    doLast {
        println(version)
    }
}
